/*
    References:
        [JIM14] Jimenez, Jorge. "Next Generation Post Processing in Call of Duty: Advanced Warfare" SIGGRAPH 2014.
        https://www.iryoku.com/next-generation-post-processing-in-call-of-duty-advanced-warfare/
*/
layout(local_size_x = 16, local_size_y = 16) in;

#include "Common.glsl"
vec4 bloom_input_up(ivec2 coord, ivec2 offset);

void main() {
    ivec2 pixelPos = ivec2(gl_GlobalInvocationID.xy);

    if (all(lessThan(pixelPos, bloom_outputSize()))) {
        // a b c
        // d e f
        // g h i
        // a,c,g,i: 1/16 (0.0625)
        // b,d,f,h: 2/16 (0.125)
        // e: 4/16 (0.25)
        vec4 result = vec4(0.0);

        vec4 a = bloom_input_up(pixelPos, ivec2(-1, -1));
        vec4 c = bloom_input_up(pixelPos, ivec2(1, -1));
        vec4 g = bloom_input_up(pixelPos, ivec2(-1, 1));
        vec4 i = bloom_input_up(pixelPos, ivec2(1, 1));
        result += (a + c + g + i) * 0.0625;

        vec4 b = bloom_input_up(pixelPos, ivec2(0, -1));
        vec4 d = bloom_input_up(pixelPos, ivec2(-1, 0));
        vec4 f = bloom_input_up(pixelPos, ivec2(1, 0));
        vec4 h = bloom_input_up(pixelPos, ivec2(0, 1));
        result += (b + d + f + h) * 0.125;

        vec4 e = bloom_input_up(pixelPos, ivec2(0, 0));
        result += e * 0.25;

        bloom_output(pixelPos, result);
    }
}