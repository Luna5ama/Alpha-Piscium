// Contains code adopted from:
// https://github.com/sebh/UnrealEngineSkyAtmosphere
// MIT License
// Copyright (c) 2020 Epic Games, Inc.
//
// You can find full license texts in /licenses
#include "../_Util.glsl"
#include "Common.glsl"

layout(local_size_x = 64, local_size_y = 1, local_size_z = 2) in;
const ivec3 workGroups = ivec3(1, 64, 1);

layout(rgba16f) restrict uniform image2D uimg_skyLUT;

uniform sampler2D usam_transmittanceLUT;
uniform sampler2D usam_multiSctrLUT;

shared vec3 shared_resultMoon[64];

// originView: ray origin in view space
// endView: ray end in view space
ScatteringResult computeSingleScattering(AtmosphereParameters atmosphere, vec3 rayDir) {
    ScatteringResult result = ScatteringResult(vec3(1.0), vec3(0.0));
    if (all(equal(rayDir, vec3(0.0)))) {
        return result;
    }

    vec3 originView = vec3(0.0, 0.0, 0.0);

    RaymarchParameters params;
    params.rayStart = atmosphere_viewToAtm(atmosphere, originView);

    vec3 lightDirWorld = gl_LocalInvocationID.z == 1 ? uval_moonDirWorld : uval_sunDirWorld;

    params.cosZenith = dot(lightDirWorld, vec3(0.0, 1.0, 0.0));
    float cosLightTheta = -dot(rayDir, lightDirWorld);
    params.rayleighPhase = rayleighPhase(cosLightTheta);
    params.miePhase = miePhase(cosLightTheta, atmosphere.miePhaseG);
    params.steps = 64u;

    vec3 earthCenter = vec3(0.0);

    // Check if ray origin is outside the atmosphere
    if (length(params.rayStart) > atmosphere.top) {
        float tTop = raySphereIntersectNearest(params.rayStart, rayDir, earthCenter, atmosphere.top);
        if (tTop < 0.0) {
            return result; // No intersection with atmosphere: stop right away
        }
        params.rayStart += rayDir * (tTop + PLANET_RADIUS_OFFSET);
    }

    float tBottom = raySphereIntersectNearest(params.rayStart, rayDir, earthCenter, atmosphere.bottom);
    float tTop = raySphereIntersectNearest(params.rayStart, rayDir, earthCenter, atmosphere.top);
    float rayLen = 0.0;

    if (tBottom < 0.0) {
        if (tTop < 0.0) {
            return result; // No intersection with earth nor atmosphere: stop right away
        } else {
            rayLen = tTop;
        }
    } else {
        if (tTop > 0.0) {
            rayLen = min(tTop, tBottom);
        }
    }

    params.rayEnd = params.rayStart + rayDir * rayLen;

    result = raymarchSingleScattering(atmosphere, params, usam_transmittanceLUT, usam_multiSctrLUT);
    result.inScattering *= global_sunRadiance.rgb * global_sunRadiance.a;
    result.inScattering *= gl_LocalInvocationID.z == 0 ? vec3(1.0) : MOON_RADIANCE_MUL;

    return result;
}

void main() {
    ivec2 imgSize = imageSize(uimg_skyLUT);
    ivec2 pixelPos = ivec2(gl_GlobalInvocationID.xy);

    if (all(lessThan(pixelPos, imgSize))) {
        vec2 texCoord = (pixelPos + 0.5 + global_taaJitter) / vec2(imgSize);
        AtmosphereParameters atmosphere = getAtmosphereParameters();
        vec3 rayDir = coords_octDecode01(texCoord);

        ScatteringResult result = computeSingleScattering(atmosphere, rayDir);

        vec3 color = result.inScattering * PI;

        if (gl_LocalInvocationID.z == 1) {
            shared_resultMoon[gl_LocalInvocationID.x] = color;
        }

        barrier();

        if (gl_LocalInvocationID.z == 0) {
            color += shared_resultMoon[gl_LocalInvocationID.x];
            imageStore(uimg_skyLUT, pixelPos, vec4(color, 1.0));
        }
    }
}